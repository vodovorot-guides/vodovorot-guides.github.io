---
title: Полезные макросы для шамана
layout: page
last_update: 2020-10-14 
wow: 9.0.1
toc: true
author: Amani
---

# Все специализации

* Универсальный маусовер макрос для атакующих заклинаний. Если у вас есть противник под курсором – кинет в него, иначе в вашу цель. Удобно развешивать {{ site.data.spells.fs }} не меняя текущей цели. Можно использовать для таких заклинаний как {{ site.data.spells.lb }}, {{ site.data.spells.lvb }}, {{ site.data.spells.purge }} и других:
~~~
#showtooltip  
/cast [@mouseover,harm,exists][] Огненный шок
~~~

* Накладывание дебаффа по фокусу, а если фокус отсутствует – по фрейму босса. Полезно на энкаунтерах, где нужно поддерживать {{ site.data.spells.fs }} на 2 целях, атакуя по приоритету одну из них:
~~~
#showtooltip 
/cast [@focus,exists,nodead,harm] [@boss1,exists,nodead,harm] Огненный шок
~~~

* Этот макрос позволяет кинуть {{ site.data.spells.fs }} на соседнюю цель, не меняя вашей текущей. Выбирается ближайшая к вам цель, в сторону которой направлена камера:
~~~
#showtooltip  
/targetenemy  
/cast Огненный шок  
/targetlasttarget  
~~~

* Маусовер макрос для лечащих заклинаний. Можно использовать для таких заклинаний как {{ site.data.spells.hs }}, {{ site.data.spells.cleance }} и других:  
~~~
#showtooltip 
/cast [@mouseover,noharm,exists] [@target,noharm,exists] [@player] Исцеляющий всплеск
~~~

* Позволяет вызвать  {{ site.data.spells.fe }} / {{ site.data.spells.igor }} без наличия вражеской цели. Не пуллит босса при использовании!  
~~~
#showtooltip
/cast  [@=target,harm,exists] [@player] Элементаль огня
~~~
~~~
#showtooltip
/cast  [@=target,harm,exists] [@player] Элементаль земли
~~~

* Применяет {{ site.data.spells.wrt }} прямо прямо под вашего персонажа, позволяя сразу получить бонус скорости и не терять времени на установку тотема:  
~~~
#showtooltip
/cast [@player] Тотем ветряного порыва
~~~

* Позволяет вынести {{ site.data.spells.rez }} на панель заклинаний чтобы следить за временем восстановления:  
~~~
#showtooltip Реинкарнация
~~~

* Вне боя использует {{ site.data.spells.ressurect }} (для Рестора замените на {{ site.data.spells.mass_res }}), а в бою – инженерский батл-рес {{ site.data.spells.engi_res }}: 
~~~
#showtooltip
/use [combat] Нестабильный манипулятор темпорального потока; [nocombat] Дух предков
~~~

# Стихии

* Позволяет кастовать АоЕ заклинания (например {{ site.data.spells.quake }} и {{ site.data.spells.rain }}) сразу на место под курсором, избавляя от лишнего клика мышкой:  
~~~
#showtooltip
/cast [@cursor] Землетрясение
~~~

* Использование {{ site.data.spells.meteor }}, {{ site.data.spells.eye_storm }} или {{ site.data.spells.harden_skin }} одной кнопкой, в зависимости от вызванного вами элементаля с талантом {{ site.data.spells.primal_elem }}. При взятом таланте {{ site.data.spells.if }} использует её:  
~~~
#showtooltip
/cast [@mouseover,exists][] [talent:6/2] [pet] Метеорит
/cast [@mouseover,harm,exists][] [talent:6/2] [pet] Око шторма
/cast [talent:6/2] [pet] Каменные доспехи
/cast [@mouseover,harm,exists][] [talent:6/3] Ледяная ярость
~~~

* В зависимости от выбранного таланта использует {{ site.data.spells.ecs }} или {{ site.data.spells.eb }}:  
~~~
#showtooltip
/cast [talent:2/2] Вторящий шок
/cast [talent:2/3] Удар духов стихий
~~~

* В зависимости от выбранного таланта использует {{ site.data.spells.sk }} или {{ site.data.spells.asc_ele }}:  
~~~
#showtooltip
/cast [talent:7/2] Хранитель бурь
/cast [talent:7/3] Перерождение
~~~

# Совершенствование

* Использование {{ site.data.spells.ww }} и {{ site.data.spells.ftw }} на одной кнопке:  
~~~
#showtooltip
/castsequence Оружие неистовства ветра, Оружие языка пламени
~~~

* В зависимости от выбранного таланта использует {{ site.data.spells.sk_enh }} или {{ site.data.spells.sunder }}:  
~~~
#showtooltip
/cast [talent:6/2] Хранитель бурь
/cast [talent:6/3] Раскол
~~~

* В зависимости от выбранного таланта использует {{ site.data.spells.spike }} или {{ site.data.spells.asc_enh }}:  
~~~
#showtooltip
/cast [talent:7/2] Земляной шип
/cast [talent:7/3] Перерождение
~~~

# Исцеление

* Маусовер макрос, который позволяет назначить на одну кнопку два действия – для врага и для союзника. Можно использовать на {{ site.data.spells.riptide }} + {{ site.data.spells.fs }}, {{ site.data.spells.dispel }} + {{ site.data.spells.purge }} и другие:
~~~
/use [@mouseover,nodead,harm]Огненный шок;[@mouseover,nodead,help]Быстрина;[@target,nodead,harm]Огненный шок;[@target,nodead,help][@player] Быстрина
~~~

* Маусовер для атакующих заклинания для Рестора, позволяет атаковать даже в цель союзника под маусовером. Не рекомендуется использовать в ПвП:  
~~~
#showtooltip 
/cast [@mouseover, exists, harm, nodead] [@mouseovertarget, exists, harm, nodead] [@targettarget, exists, harm, nodead] [exists, harm, nodead] Молния
~~~

* В зависимости от выбранного таланта использует {{ site.data.spells.ewt }} или {{ site.data.spells.apt }} на землю под курсором:  
~~~
#showtooltip
/cast [@cursor] [talent:4/2] Тотем земляной стены
/cast [@cursor] [talent:4/3] Тотем защиты предков
~~~

* В зависимости от выбранного таланта использует {{ site.data.spells.wellspring }} или {{ site.data.spells.asc_resto }}:  
~~~
#showtooltip
/cast [talent:7/2] Родник
/cast [talent:7/3] Перерождение
~~~

# Универсальные и полезные макросы

* Основной макрос для взятия цели в фокус. Первое нажатие берет в фокус цель под маусовером или в таргете. Если нажать без маусовера или таргета, то очищает фокус:  
~~~
/focus [target=mouseover,exists][exists]
/stopmacro [target=mouseover,exists][exists]
/clearfocus
~~~

* Ставит на землю метку «Череп». Меняйте цифру на конце макроса для других меток:  
~~~
/click CompactRaidFrameManagerDisplayFrameLeaderOptionsRaidWorldMarkerButton
/click DropDownList1Button1
~~~

* Стереть все метки, поставленные на землю:  
~~~
/click CompactRaidFrameManagerDisplayFrameLeaderOptionsRaidWorldMarkerButton
/click DropDownList1Button9
~~~

* Использовать одновременно {{ site.data.spells.waterwalking }} (без ГКД) и призыв случайного избранного маунта: 
~~~
#showtooltip Яростная буря предсказателя
/cast [nomounted,nocombat] Хождение по воде
/run C_MountJournal.SummonByID(0)
~~~

* Три маунта одной кнопкой, в зависимости от вашего местонахождения. Замените названия на ваших любимых маунтов:
~~~
#showtooltip 
/cast [nomounted,nocombat] Хождение по воде
/cast [swimming] Багровый бурунный конек; [noflyable] Механоцикл модели W;   [flyable] Яростная буря предсказателя;
/dismount [mounted]
~~~

* Способности через модификатор:  
~~~
#showtooltip
/use [mod:ctrl] умение 4;[mod:alt] умение 3;[mod:shift] умение 2;умение 1
~~~
~~~
#showtooltip
/use [mod:shift] Цепное исцеление; Цепная молния
~~~

> Одна из самых популярных разновидностей макросов. По сути – просто сокращается количество использованных кнопок на панелях заклинаний. Скрытые панели решают эту проблему.  


* Использование активных (прожимных) Аксессуаров с панели заклинаний:  
~~~
#showtooltip
/use 13
~~~
~~~
#showtooltip
/use 14
~~~

* Использование способности на поясе у инженеров:  
~~~
#showtooltip
/use 6
~~~

* Использование инженерского {{ site.data.spells.planer }}:  
~~~
#showtooltip
/cancelaura Нитроускорители
/use 15
~~~

# Модификаторы цели

Символ «@» равносилен «target=».

* **@player** – нацеливается на игрока.
* **@target** – нацеливается на текущую цель игрока. 
* **@targettarget** – нацеливается на цель цели. Например, при использовании цели на босса, она будет нацелена на танка. Данный модификатор можно стакать, выбирая цель цели цели цели цели и т.д.
* **@mouseover** – нацеливает заклинание на цель, над которой в данный момент находится курсор мыши.
* **@cursor** – использование заклинаний по области, в то место, на котором в данный момент находится курсор (доступно только для заклинаний с применением на область, например {{ site.data.spells.quake }} или {{ site.data.spells.rain }}).
* **@focus** – нацеливается на цель у вас в фокусе.
* **@boss1** – нацеливается на первого босса в списке боссов. Обычно доступно только в подземельях и рейдах. 
* **@arena1** – нацеливается на первого человека в списке Арены. Это модификатор только для PvP, обычно доступный только во время боев на арене. Есть еще варианты, такие как arena2, arena3 и т. д.
* **@pet** – нацеливается на питомца игрока.

# Условия

Условные выражения используются для определения возможности действия. Чтобы они вели себя противоположным образом, добавьте «no» перед условным выражением. Например, noharm верно для любой цели, которая не является врагом, но может включать например  нейтральных NPC, а nocombat верно если вы не находитесь в бою.

Полный список условий:

* **pet** – верно, если у игрока в данный момент есть питомец.
* **combat** – верно, если вы в бою.
* **harm** – верно, если целью для использования способности является враг.
* **help** – верно, если целью для использования способности является союзник.
* **dead** – верно, если цель для использования способности мертва.
* **exists** – верно, если цель для использования способности существует.
* **channeling** – верно, если вы в данный момент используете потоковое заклинание.
* **button:number** – (или btn) используется для проверки, с помощью какой кнопки мыши вы активировали макрос. [button:1] по умолчанию, левая кнопка, [button:2] правая, [кнопка:3] средняя. Для мышей с большим количеством кнопок количество кнопок соответствует шаблону для опции.
* **talent:Row(Строка)/Column(Колонка)** – верно, если у вас выбран талант Row/Column. Например, [талант: 7/2] будет верным, если у вас выбранный средний  талант в последнем тире.
* **equipped:slot** – (или надето) верно, если в слоте есть предмет. Например, [equipped:chest] будет верным, если на вас надета нагрудная броня.
* **spec:number** – верно, если вы в настоящее время находитесь в специализации, заданной числом. Специализации отсортированы в алфавитном порядке и могут быть проверены в меню специализаций.
* **group** – верно, если вы в данный момент находитесь в группе. Может быть перестроен в [group:party] или [group:raid].
* **indoors**  – верно, если вы в настоящее время находитесь в локации, которая считается «закрытой», например помещением.
* **outdoors**  – верно, если вы в настоящее время находитесь в локации, которая считается на «открытой», под открытым небом.
* **flyable** – верно, если вы в данный момент можете летать.
* **mounted**  – верно, если вы в настоящее время на средстве передвижения.
* **fly** – верно, если вы в данный момент летите.
* **swimming** – верно, если вы в данный момент плаваете.

# Модификаторы комбинаций клавиш

* **mod:shift** – верно, если комбинация клавиш была нажата при нажатой клавише SHIFT.
* **mod:alt** – верно, если комбинация клавиш была нажата при нажатой клавише ALT.
* **mod:ctrl** – верно, если комбинация клавиш была нажата при нажатой клавише CTRL.

